{
    parserClass="org.dnltsk.mapfileplugin.parser.MapfileParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Mapfile"
    psiImplClassSuffix="Impl"
    psiPackage="org.dnltsk.mapfileplugin.psi"
    psiImplPackage="org.dnltsk.mapfileplugin.psi.impl"

    elementTypeHolderClass="org.dnltsk.mapfileplugin.psi.MapfileTypes"
    elementTypeClass="org.dnltsk.mapfileplugin.psi.MapfileElementType"
    tokenTypeClass="org.dnltsk.mapfileplugin.psi.MapfileTokenType"

    psiImplUtilClass="org.dnltsk.mapfileplugin.psi.impl.MapfilePsiImplUtil"

  tokens=[
    SEMI=';'
    EQ='='
    LP='('
    RP=')'

    attributeToken="regexp:\[.+\]"
    logicalExpressionToken="regexp:\(.+\)"
    listExpressionToken="regexp:\{.+\}"
    regexToken="regexp:/.+/"
    integer='regexp:[+-]{0,1}\d+'
    double='regexp:[+-]{0,1}\d+(\.\d*)?'

    space='regexp:\s+'
    comment='regexp:#.*'
    number='regexp:[+-]{0,1}\d+(\.\d*)?'
    id='regexp:\p{Alpha}\w*'
    string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"

    op_1='+'
    op_2='-'
    op_3='*'
    op_4='/'
    op_5='!'
  ]
}

mapfile ::= (MapObject | LayerObject)

//
// OBJECTS
//
private MapObject ::= MAP MapObjectChildren END
private MapObjectChildren ::=
    (
        LayerObject | LegendObject | ProjectionObject | QuerymapObject | ReferenceObject | ScalebarObject
        | SymbolObject | WebObject
        | AngleAttr | ConfigAttr | DatapatternAttr | DebugAttr | DefresolutionAttr | ExtentAttr | FontsetAttr
        | ImagecolorAttr | ImagetypeAttr | InterlaceAttr | MaxsizeAttr | NameAttr | ResolutionAttr
        | ScaledenomAttr | ShapepathAttr | SizeAttr | StatusAttr | SymbolsetAttr | TemplatepatternAttr | UnitsAttr

    ) MapObjectChildren*

private WebObject ::= WEB WebObjectChildren END
private WebObjectChildren ::=
    (
        MetadataObject | ValidationObject
        | BrowseformatAttr | EmptyAttr | ErrorAttr | FooterAttr | HeaderAttr | ImagepathAttr | ImageurlAttr
        | LegendformatAttr | MaxscaledenomAttr | MaxtemplateAttr | MinscaledenomAttr | MintemplateAttr
        | QueryformatAttr | TemplateAttr | TemppathAttr
    ) WebObjectChildren*

private LayerObject ::= LAYER LayerObjectChildren END
private LayerObjectChildren ::=
    (
        ClassObject | ClusterObject | CompositeObject | FeatureObject | GridObject | JoinObject | MetadataObject
        | ProjectionObject | ValidationObject
        | ClassgroupAttr | ClassitemAttr | ConnectionAttr | ConnectiontypeAttr | DataAttr | DebugAttr | DumpAttr
        | EncodingAttr | ExtentAttr | FilterAttr | FooterAttr | FilteritemAttr | GeomtransformAttr | GroupAttr
        | HeaderAttr | LabelcacheAttr | LabelitemAttr | LabelmaxscaledenomAttr | LabelminscaledenomAttr
        | LabelrequiresAttr | MaskAttr | MaxfeaturesAttr | MaxgeowidthAttr | MaxscaledenomAttr | MingeowidthAttr
        | MinscaledenomAttr | NameAttr | PluginAttr | PostlabelcacheAttr | ProcessingAttr | OffsiteAttr | SizeunitsAttr
        | RequiresAttr | StatusAttr | StyleitemAttr | SymbolscaledenomAttr | TemplateAttr | TileindexAttr
        | TileitemAttr | TilesrsAttr | TitleAttr | ToleranceunitsAttr | ToleranceAttr | TransformAttr | TypeAttr
        | UnitsAttr | UtfdataAttr | UtfitemAttr

    ) LayerObjectChildren*

private ClassObject ::= CLASS ClassObjectChildren END
private ClassObjectChildren ::=
    (
        LabelObject | LeaderObject | StyleObject | ValidationObject
        | DebugClassAttr | ExpressionAttr | GroupAttr | KeyimageAttr
        | MaxscaledenomAttr | MinscaledenomAttr | NameAttr | StatusAttr | TemplateAttr | TextAttr
    ) ClassObjectChildren*

private SymbolObject ::= SYMBOL SymbolObjectChildren END
private SymbolObjectChildren ::=
    (
        PointsObject
        | AnchorpointAttr | AntialiasAttr | CharacterAttr | FilledAttr | FontAttr | ImageAttr | NameAttr
        | TransparentAttr | TypeSymbolAttr
    )

private LabelObject ::= LABEL LabelObjectChildren END
private LabelObjectChildren ::=
    (
        StyleObject
        | AlignAttr | AngleLabelAttr | AntialiasAttr | BufferAttr | ColorClassEnum | ExpressionAttr | FontLabelAttr
        | ForceAttr | MaxlengthAttr | MaxoverlapangleAttr | MaxscaledenomAttr | MaxsizeAttr | MindistanceAttr
        | MinfeaturesizeAttr | MinscaledenomAttr | MinsizeAttr | OffsetAttr | OutlinecolorClassAttr | OutlinewidthAttr
        | PartialsAttr | PositionAttr | PriorityAttr | RepeatdistanceAttr | ShadowcolorAttr | ShadowsizeAttr
        | SizeLabelAttr | TextAttr | TypeLabelAttr | WrapAttr
    )

private StyleObject ::= STYLE StyleObjectChildren END
private StyleObjectChildren ::=
    (
        PatternObject
        | AngleClassAttr | AntialiasAttr | ColorClassAttr | GapAttr | GeomtransformClassAttr | InitialgapAttr
        | LinecapAttr | LinejoinAttr | LinejoinmaxsizeAttr | MaxscaledenomAttr | MaxsizeAttr | MaxwidthAttr
        | MinscaledenomAttr | MinsizeAttr | MinwidthAttr | OffsetAttr | OpacityClassAttr | OutlinecolorClassAttr
        | OutlinewidthAttr | PolaroffsetAttr | SizeClassAttr | SymbolClassAttr | WidthClassAttr
    )

private LeaderObject ::= LEADER LeaderObjectChildren END // TODO supported layer type: Polygon
private LeaderObjectChildren ::=
    (
        StyleObject
        | GridstepAttr | MaxdistanceAttr
    )

private LegendObject ::= LEGEND LegendObjectChildren END
private LegendObjectChildren ::=
    (
        LabelObject
        | ImagecolorAttr | InterlaceAttr | KeysizeAttr | KeyspacingAttr | OutlinecolorAttr | PositionAttr
        | PostlabelcacheAttr | StatusAttr | TemplateAttr
    ) LegendObjectChildren*

private FeatureObject ::= FEATURE FeatureObjectChildren END
private FeatureObjectChildren ::=
    (
        PointsObject
        | ItemsAttr | TextAttr | WktAttr
    ) FeatureObjectChildren*

private ClusterObject ::= CLUSTER ClusterObjectChildren END // TODO: only within POINT layers
private ClusterObjectChildren ::=
    (
        MaxdistanceAttr | RegionAttr | BufferAttr | GroupAttr | FilterAttr | ProcessingAttr
    ) ClusterObjectChildren*

private CompositeObject ::= COMPOSITE CompositeObjectChildren END
private CompositeObjectChildren ::=
    (
        OpacityAttr | CompopAttr
    ) CompositeObjectChildren*

private GridObject ::= GRID GridObjectChildren END
private GridObjectChildren ::=
    (
        LabelformatAttr | MinarcsAttr | MaxarcsAttr | MinintervalAttr | MaxintervalAttr | MinsubdivideAttr
        | MaxsubdivideAttr
    ) GridObjectChildren*

private JoinObject ::= JOIN JoinObjectChildren END
private JoinObjectChildren ::=
    (
        ConnectionAttr | ConnectiontypeJoinAttr | FooterAttr |  FromAttr | HeaderAttr | NameAttr | TableAttr
        | TemplateAttr | ToAttr | TypeJoinAttr
    ) JoinObjectChildren*

private QuerymapObject ::= QUERYMAP QuerymapObjectChildren END
private QuerymapObjectChildren ::=
    (
        ColorAttr | SizeAttr | StatusAttr | StyleQuerymapAttr
    ) QuerymapObjectChildren*

private ReferenceObject ::= REFERENCE ReferenceObjectChildren END
private ReferenceObjectChildren ::=
    (
        ColorAttr | ExtentAttr | ImageAttr | MarkerAttr | MarkersizeAttr | MinboxsizeAttr | MaxboxsizeAttr
        | OutlinecolorAttr | SizeAttr | StatusAttr
    ) ReferenceObjectChildren*

private ScalebarObject ::= SCALEBAR ScalebarObjectChildren END
private ScalebarObjectChildren ::=
    (
        LabelObject
        | AlignAttr | BackgroundcolorAttr | ColorAttr | ImagecolorAttr | InterlaceAttr | OutlinecolorAttr | PositionAttr
        | PostlabelcacheAttr | SizeAttr | StatusAttr | StyleScalebarAttr | UnitsAttr
    ) ScalebarObjectChildren*

private PatternObject ::= PATTERN ( integer integer )* END

private ValidationObject ::= VALIDATION ( string string )* END

private MetadataObject ::= METADATA ( string string )* END

private PointsObject ::= POINTS ( double double )* END

private ProjectionObject ::= PROJECTION string* END

//
// ATTRIBUTES
//
private AlignAttr ::= ALIGN AlignEnum
private AnchorpointAttr ::= ANCHORPOINT double double // double 0-1
private AngleAttr ::= ANGLE ( integer | double )
private AngleClassAttr ::= ANGLE AngleClassEnum
private AngleLabelAttr ::= ANGLE AngleLabelEnum
private AntialiasAttr ::= ANTIALIAS BooleanEnum
private BackgroundcolorAttr ::= BACKGROUNDCOLOR ColorEnum //TODO color
private BrowseformatAttr ::= BROWSEFORMAT string
private BufferAttr ::= BUFFER ( integer | double )
private CharacterAttr ::= CHARACTER string // TODO char
private ClassgroupAttr ::= CLASSGROUP string
private ClassitemAttr ::= CLASSITEM string
private ColorAttr ::= COLOR ColorEnum //TODO color
private ColorClassAttr ::= COLOR ColorClassEnum
private ConfigAttr ::= CONFIG (
    ( CGI_CONTEXT_URL string )
    | ( MS_ENCRYPTION_KEY string )
    | ( MS_ERRORFILE string )
    | ( MS_NONSQUARE YesNoEnum )
    | ( ON_MISSING_DATA OnMissingDataEnum )
    | ( PROJ_LIB string ) )
private CompopAttr ::= COMPOP string // TODO: keywords in string
private ConnectionAttr ::= CONNECTION string
private ConnectiontypeAttr ::= CONNECTIONTYPE ConnectiontypeEnum
private ConnectiontypeJoinAttr ::= CONNECTIONTYPE ConnectiontypeJoinEnum
private DataAttr ::= DATA string
private DatapatternAttr ::= DATAPATTERN string // TODO: regular expression
private DebugAttr ::= DEBUG DebugEnum
private DebugClassAttr ::= DEBUG BooleanEnum
private DefresolutionAttr ::= DEFRESOLUTION integer
private DumpAttr ::= DUMP BooleanEnum
private EmptyAttr ::= EMPTY string // TODO: url
private EncodingAttr ::= ENCODING string
private ErrorAttr ::= ERROR string // TODO: url
private ExpressionAttr ::= EXPRESSION ExpressionEnum
private ExtentAttr ::= EXTENT ( integer | double ) ( integer | double ) ( integer | double ) ( integer | double )
private FilledAttr ::= FILLED BooleanEnum
private FilterAttr ::= FILTER ExpressionEnum // TODO: expression!
private FilteritemAttr ::= FILTERITEM string
private FontAttr ::= FONT string // TODO must be defined in fontset!
private FontLabelAttr ::= FONT ( string | attributeToken ) // TODO must be defined in fontset!
private FontsetAttr ::= FONTSET string // TODO: file
private FooterAttr ::= FOOTER string // TODO: filename
private ForceAttr ::= FORCE BooleanEnum
private FromAttr ::= FROM string
private GapAttr ::= GAP double
private GeomtransformAttr ::= GEOMTRANSFORM string // TODO: [<expression>|<Javascript file>]
private GeomtransformClassAttr ::= GEOMTRANSFORM GeomtransformClassEnum
private GridstepAttr ::= GRIDSTEP integer
private GroupAttr ::= GROUP string
private HeaderAttr ::= HEADER string // TODO: filename
private ImageAttr ::= IMAGE string
private ImagecolorAttr ::= IMAGECOLOR ColorEnum // TODO: hex or RGB
private ImagepathAttr ::= IMAGEPATH string // TODO: path
private ImagetypeAttr ::= IMAGETYPE ImagetypeEnum // TODO: must match one of the defined ones in OutputfomatObject
private ImageurlAttr ::= IMAGEURL string // TODO path? url?
private InitialgapAttr ::= INITIALGAP integer
private InterlaceAttr ::= INTERLACE OnOffEnum
private ItemsAttr ::= ITEMS string // TODO: comma separated list
private KeyimageAttr ::= KEYIMAGE string // TODO filename
private KeysizeAttr ::= KEYSIZE integer integer
private KeyspacingAttr ::= KEYSPACING integer integer
private LabelcacheAttr ::= LABELCACHE OnOffEnum
private LabelformatAttr ::= LABELFORMAT LabelFormatEnum
private LabelitemAttr ::= LABELITEM string
private LabelmaxscaledenomAttr ::= LABELMAXSCALEDENOM ( integer | double )
private LabelminscaledenomAttr ::= LABELMINSCALEDENOM ( integer | double )
private LabelrequiresAttr ::= LABELREQUIRES string // TODO: expression
private LegendformatAttr ::= LEGENDFORMAT string
private LinecapAttr ::= LINECAP LinecapEnum
private LinejoinAttr ::= LINEJOIN LinejoinEnum
private LinejoinmaxsizeAttr ::= LINEJOINMAXSIZE integer
private MarkerAttr ::= MARKER ( integer | string)
private MarkersizeAttr ::= MARKERSIZE integer
private MaskAttr ::= MASK string // TODO: layername validation
private MaxarcsAttr ::= MAXARCS double
private MaxboxsizeAttr ::= MAXBOXSIZE integer
private MaxdistanceAttr ::= MAXDISTANCE ( integer | double )
private MaxfeaturesAttr ::= MAXFEATURES integer
private MaxgeowidthAttr ::= MAXGEOWIDTH ( integer | double )
private MaxintervalAttr ::= MAXINTERVAL ( integer | double )
private MaxlengthAttr ::= MAXLENGTH integer
private MaxoverlapangleAttr ::= MAXOVERLAPANGLE ( integer | double )
private MaxscaledenomAttr ::= MAXSCALEDENOM ( integer | double )
private MaxsizeAttr ::= MAXSIZE integer
private MaxsubdivideAttr ::= MAXSUBDIVIDE ( integer | double )
private MaxtemplateAttr ::= MAXTEMPLATE string // TODO: [file/url]
private MaxwidthAttr ::= MAXWIDTH ( integer | double )
private MinarcsAttr ::= MINARCS ( integer | double )
private MinboxsizeAttr ::= MINBOXSIZE integer
private MindistanceAttr ::= MINDISTANCE ( integer | double )
private MinfeaturesizeAttr ::= MINFEATURESIZE MinfeaturesizeEnum
private MingeowidthAttr ::= MINGEOWIDTH ( integer | double )
private MinintervalAttr ::= MININTERVAL ( integer | double )
private MinscaledenomAttr ::= MINSCALEDENOM ( integer | double )
private MinsizeAttr ::= MINSIZE integer
private MinsubdivideAttr ::= MINSUBDIVIDE ( integer | double )
private MintemplateAttr ::= MINTEMPLATE string // TODO: [file/url]
private MinwidthAttr ::= MINWIDTH ( integer | double )
private NameAttr ::= NAME string // TODO: Unique check
private OffsetAttr ::= OFFSET ( integer | double ) ( integer | double )
private OffsiteAttr ::= OFFSITE ColorEnum
private OpacityAttr ::= OPACITY integer // TODO: 0-100
private OpacityClassAttr ::= OPACITY ( integer | attributeToken )
private OutlinecolorAttr ::= OUTLINECOLOR ColorEnum // TODO color
private OutlinecolorClassAttr ::= OUTLINECOLOR ColorClassEnum
private OutlinewidthAttr ::= OUTLINEWIDTH ( double | attributeToken )
private PartialsAttr ::= PARTIALS BooleanEnum
private PluginAttr ::= PLUGIN string // TODO: filename
private PolaroffsetAttr ::= POLAROFFSET ( ( double | attributeToken ) ( double | attributeToken ) )
private PositionAttr ::= POSITION PositionEnum
private PostlabelcacheAttr ::= POSTLABELCACHE BooleanEnum
private PriorityAttr ::= PRIORITY ( integer | string | attributeToken )
private ProcessingAttr ::= PROCESSING string // TODO: keywords in string!!
private QueryformatAttr ::= QUERYFORMAT string
private RegionAttr ::= REGION string
private RepeatdistanceAttr ::= REPEATDISTANCE integer
private RequiresAttr ::= REQUIRES string // TODO: expression
private ResolutionAttr ::= RESOLUTION integer
private ScaledenomAttr ::= SCALEDENOM ( integer | double )
private ShadowcolorAttr ::= SHADOWCOLOR ColorEnum
private ShadowsizeAttr ::= SHADOWSIZE ( ( integer | attributeToken ) ( integer | attributeToken ) )
private ShapepathAttr ::= SHAPEPATH string // TODO: directory
private SizeAttr ::= SIZE integer integer
private SizeClassAttr ::= SIZE ( double | attributeToken )
private SizeLabelAttr ::= SIZE SizeLabelEnum
private SizeunitsAttr ::= SIZEUNITS SizeUnitEnum
private StatusAttr ::= STATUS StatusEnum
private StyleitemAttr ::= STYLEITEM string // TODO: [<attribute>|auto|<javascript file>]
private StyleQuerymapAttr ::= STYLE StyleQuerymapEnum
private StyleScalebarAttr ::= STYLE ( 0 | 1)
private SymbolClassAttr ::= SYMBOL (integer | string | attributeToken)
private SymbolscaledenomAttr ::= SYMBOLSCALEDENOM double
private SymbolsetAttr ::= SYMBOLSET string  // TODO: file
private TableAttr ::= TABLE string
private TemplateAttr ::= TEMPLATE string // TODO: [file|url]
private TemplatepatternAttr ::= TEMPLATEPATTERN string // TODO regex
private TemppathAttr ::= TEMPPATH string // TODO: path
private TextAttr ::= TEXT string // TODO string,expression
private TileindexAttr ::= TILEINDEX string // TODO: [filename|layername]
private TileitemAttr ::= TILEITEM string // TODO: [attribute]
private TilesrsAttr ::= TILESRS string // TODO: [attribute]
private TitleAttr ::= TITLE string
private ToAttr ::= TO string
private ToleranceAttr ::= TOLERANCE ( integer | double )
private ToleranceunitsAttr ::= TOLERANCEUNITS ToleranceUnitEnum
private TransformAttr ::= TRANSFORM TransformEnum
private TransparentAttr ::= TRANSPARENT integer // TODO color-index
private TypeAttr ::= TYPE TypeEnum
private TypeJoinAttr ::= TYPE TypeJoinEnum
private TypeLabelAttr ::= TYPE TypeLabelEnum
private TypeSymbolAttr ::= TYPE TypeSymbolEnum
private UnitsAttr ::= UNITS UnitsEnum
private UtfdataAttr ::= UTFDATA string
private UtfitemAttr ::= UTFITEM string
private WidthClassAttr ::= WIDTH ( ( integer | double ) | attributeToken )
private WktAttr ::= WKT string // TODO wkt standard
private WrapAttr ::= WRAP string // TODO character

//
// ENUMERATIONS
//
private AlignEnum ::= left|center|right
private AngleClassEnum ::= integer|double|attributeToken|auto
private AngleLabelEnum ::= AngleClassEnum|auto2|follow
private BooleanEnum ::= true|false
private ColorClassEnum ::= ColorEnum|attributeToken
private ColorEnum ::= string | ( integer integer integer )
private ConnectiontypeEnum ::= contour|kerneldensity|local|ogr|oraclespatial|plugin|postgis|sde|union|uvraster|wfs|wms // TODO check for completeness
private ConnectiontypeJoinEnum ::= csv|mysql|postgresql
private DebugEnum ::= OnOffEnum|0|1|2|3|4|5
private ExpressionEnum ::= ( string | logicalExpressionToken | regexToken | listExpressionToken ) // TODO string functions
private GeomtransformClassEnum ::= bbox|centroid|end|labelpnt|labelpoly|start|vertices|string // TODO string = expression!
private ImagetypeEnum ::= png|gif|png8|jpeg|svg|pdf|gtiff|kml|kmz|cairopng|string
private LabelFormatEnum ::= dd|ddmm|ddmmss|string // TODO string=cformat-string
private LinecapEnum ::= butt|round|square
private LinejoinEnum ::= round|miter|bevel|none
private MinfeaturesizeEnum ::= integer|auto
private OnMissingDataEnum ::= fail|log|ignore
private OnOffEnum ::= on|off
private PositionEnum ::= ul|uc|ur|cl|cc|cr|ll|lc|lr // TODO why not cl\cc\cr?
private SizeLabelEnum ::= integer|tiny|small|medium|large|giant|attributeToken
private SizeUnitEnum ::= feet|inches|kilometers|meters|miles|nauticalmiles|pixels
private StatusEnum ::= OnOffEnum|default
private StyleQuerymapEnum ::= normal|hilite|selected
private ToleranceUnitEnum ::= SizeUnitEnum|dd
private TransformEnum ::= BooleanEnum|ul|uc|ur|cl|cc|cr|ll|lc|lr
private TypeEnum ::= chart|circle|line|point|polygon|raster|query
private TypeJoinEnum ::= ONE-TO-ONE|ONE-TO-MANY
private TypeLabelEnum ::= bitmap|truetype
private TypeSymbolEnum ::= ellipse|hatch|pixmap|svg|truetype|vector
private UnitsEnum ::= ToleranceUnitEnum|percentages
private YesNoEnum ::= yes|no
