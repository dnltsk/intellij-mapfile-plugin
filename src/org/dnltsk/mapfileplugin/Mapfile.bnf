{
    parserClass="org.dnltsk.mapfileplugin.parser.MapfileParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Mapfile"
    psiImplClassSuffix="Impl"
    psiPackage="org.dnltsk.mapfileplugin.psi"
    psiImplPackage="org.dnltsk.mapfileplugin.psi.impl"

    elementTypeHolderClass="org.dnltsk.mapfileplugin.psi.MapfileTypes"
    elementTypeClass="org.dnltsk.mapfileplugin.psi.MapfileElementType"
    tokenTypeClass="org.dnltsk.mapfileplugin.psi.MapfileTokenType"

    psiImplUtilClass="org.dnltsk.mapfileplugin.psi.impl.MapfilePsiImplUtil"

  tokens=[
    SEMI=';'
    EQ='='
    LP='('
    RP=')'

    attributeToken="regexp:\[.+\]"
    logicalExpressionToken="regexp:\(.+\)"
    listExpressionToken="regexp:\{.+\}"
    regexToken="regexp:/.+/"
    integer='regexp:[+-]{0,1}\d+'
    double='regexp:[+-]{0,1}\d+(\.\d*)?'

    space='regexp:\s+'
    comment='regexp:#.*'
    number='regexp:[+-]{0,1}\d+(\.\d*)?'
    id='regexp:\p{Alpha}\w*'
    string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"

    op_1='+'
    op_2='-'
    op_3='*'
    op_4='/'
    op_5='!'
  ]
}

mapfile ::= (MapObject | WebObject | LayerObject | ClassObject | SymbolObject | LabelObject | StyleObject
             | LeaderObject | LegendObject | FeatureObject | ClusterObject | CompositeObject | GridObject
             | JoinObject | QuerymapObject | ReferenceObject | ScalebarObject | PatternObject ValidationObject
             | MetadataObject | PointsObject | ProjectionObject )

//
// OBJECTS
//
MapObject ::= MAP MapObjectChildren END { pin=1 recoverWhile="MapObjectChildren_recover" }
private MapObjectChildren ::=
    !"END" (
        LayerObject | LegendObject | ProjectionObject | QuerymapObject | ReferenceObject | ScalebarObject
        | SymbolObject | WebObject
        | AngleAttr | ConfigAttr | DatapatternAttr | DebugAttr | DefresolutionAttr | ExtentAttr | FontsetAttr
        | ImagecolorAttr | ImagetypeAttr | InterlaceAttr | MaxsizeAttr | NameAttr | ResolutionAttr
        | ScaledenomAttr | ShapepathAttr | SizeAttr | StatusAttr | SymbolsetAttr | TemplatepatternAttr | UnitsAttr
    )* {pin=1}
private MapObjectChildren_recover ::=
    !(
        LAYER | LEGEND | PROJECTION | QUERYMAP | REFERENCE | SCALEBAR
        | SYMBOL | WEB
        | ANGLE | CONFIG | DATAPATTERN | DEBUG | DEFRESOLUTION | EXTENT | FONTSET
        | IMAGECOLOR | IMAGETYPE | INTERLACE | MAXSIZE | NAME | RESOLUTION
        | SCALEDENOM | SHAPEPATH | SIZE | STATUS | SYMBOLSET | TEMPLATEPATTERN | UNITS
        | END
    )

WebObject ::= WEB WebObjectChildren END { pin=1 recoverWhile="WebObjectChildren_recover"}
private WebObjectChildren ::=
    !"END" (
        MetadataObject | ValidationObject
        | BrowseformatAttr | EmptyAttr | ErrorAttr | FooterAttr | HeaderAttr | ImagepathAttr | ImageurlAttr
        | LegendformatAttr | MaxscaledenomAttr | MaxtemplateAttr | MinscaledenomAttr | MintemplateAttr
        | QueryformatAttr | TemplateAttr | TemppathAttr
    )* {pin=1}
private WebObjectChildren_recover ::=
    !(
        METADATA | VALIDATION
        | BROWSEFORMAT | EMPTY | ERROR | FOOTER | HEADER | IMAGEPATH | IMAGEURL
        | LEGENDFORMAT | MAXSCALEDENOM | MAXTEMPLATE | MINSCALEDENOM | MINTEMPLATE
        | QUERYFORMAT | TEMPLATE | TEMPPATH
        | END
    )

LayerObject ::= LAYER LayerObjectChildren END { pin=1 recoverWhile="LayerObjectChildren_recover"}
private LayerObjectChildren ::=
    !"END" (
        ClassObject | ClusterObject | CompositeObject | FeatureObject | GridObject | JoinObject | MetadataObject
        | ProjectionObject | ValidationObject
        | ClassgroupAttr | ClassitemAttr | ConnectionAttr | ConnectiontypeAttr | DataAttr | DebugAttr | DumpAttr
        | EncodingAttr | ExtentAttr | FilterAttr | FooterAttr | FilteritemAttr | GeomtransformAttr | GroupAttr
        | HeaderAttr | LabelcacheAttr | LabelitemAttr | LabelmaxscaledenomAttr | LabelminscaledenomAttr
        | LabelrequiresAttr | MaskAttr | MaxfeaturesAttr | MaxgeowidthAttr | MaxscaledenomAttr | MingeowidthAttr
        | MinscaledenomAttr | NameAttr | PluginAttr | PostlabelcacheAttr | ProcessingAttr | OffsiteAttr | SizeunitsAttr
        | RequiresAttr | StatusAttr | StyleitemAttr | SymbolscaledenomAttr | TemplateAttr | TileindexAttr
        | TileitemAttr | TilesrsAttr | TitleAttr | ToleranceunitsAttr | ToleranceAttr | TransformAttr | TypeAttr
        | UnitsAttr | UtfdataAttr | UtfitemAttr
    )* {pin=1}
private LayerObjectChildren_recover ::=
    !(
        CLASS | CLUSTER | COMPOSITE | FEATURE | GRID | JOIN | METADATA
        | PROJECTION | VALIDATION
        | CLASSGROUP | CLASSITEM | CONNECTION | CONNECTIONTYPE | DATA | DEBUG | DUMP
        | ENCODING | EXTENT | FILTER | FOOTER | FILTERITEM | GEOMTRANSFORM | GROUP
        | HEADER | LABELCACHE | LABELITEM | LABELMAXSCALEDENOM | LABELMINSCALEDENOM
        | LABELREQUIRES | MASK | MAXFEATURES | MAXGEOWIDTH | MAXSCALEDENOM | MINGEOWIDTH
        | MINSCALEDENOM | NAME | PLUGIN | POSTLABELCACHE | PROCESSING | OFFSITE | SIZEUNITS
        | REQUIRES | STATUS | STYLEITEM | SYMBOLSCALEDENOM | TEMPLATE | TILEINDEX
        | TILEITEM | TILESRS | TITLE | TOLERANCEUNITS | TOLERANCE | TRANSFORM | TYPE
        | UNITS | UTFDATA | UTFITEM
        | END
    )

private ClassObject ::= CLASS ClassObjectChildren END
private ClassObjectChildren ::=
    (
        LabelObject | LeaderObject | StyleObject | ValidationObject
        | DebugClassAttr | ExpressionAttr | GroupAttr | KeyimageAttr
        | MaxscaledenomAttr | MinscaledenomAttr | NameAttr | StatusAttr | TemplateAttr | TextAttr
    )*

private SymbolObject ::= SYMBOL SymbolObjectChildren END
private SymbolObjectChildren ::=
    (
        PointsObject
        | AnchorpointAttr | AntialiasAttr | CharacterAttr | FilledAttr | FontAttr | ImageAttr | NameAttr
        | TransparentAttr | TypeSymbolAttr
    )*

private LabelObject ::= LABEL LabelObjectChildren END
private LabelObjectChildren ::=
    (
        StyleObject
        | AlignAttr | AngleLabelAttr | AntialiasAttr | BufferAttr | ColorClassEnum | ExpressionAttr | FontLabelAttr
        | ForceAttr | MaxlengthAttr | MaxoverlapangleAttr | MaxscaledenomAttr | MaxsizeAttr | MindistanceAttr
        | MinfeaturesizeAttr | MinscaledenomAttr | MinsizeAttr | OffsetAttr | OutlinecolorClassAttr | OutlinewidthAttr
        | PartialsAttr | PositionAttr | PriorityAttr | RepeatdistanceAttr | ShadowcolorAttr | ShadowsizeAttr
        | SizeLabelAttr | TextAttr | TypeLabelAttr | WrapAttr
    )*

private StyleObject ::= STYLE StyleObjectChildren END
private StyleObjectChildren ::=
    (
        PatternObject
        | AngleClassAttr | AntialiasAttr | ColorClassAttr | GapAttr | GeomtransformClassAttr | InitialgapAttr
        | LinecapAttr | LinejoinAttr | LinejoinmaxsizeAttr | MaxscaledenomAttr | MaxsizeAttr | MaxwidthAttr
        | MinscaledenomAttr | MinsizeAttr | MinwidthAttr | OffsetAttr | OpacityClassAttr | OutlinecolorClassAttr
        | OutlinewidthAttr | PolaroffsetAttr | SizeClassAttr | SymbolClassAttr | WidthClassAttr
    )*

private LeaderObject ::= LEADER LeaderObjectChildren END // TODO supported layer type: Polygon
private LeaderObjectChildren ::=
    (
        StyleObject
        | GridstepAttr | MaxdistanceAttr
    )*

private LegendObject ::= LEGEND LegendObjectChildren END
private LegendObjectChildren ::=
    (
        LabelObject
        | ImagecolorAttr | InterlaceAttr | KeysizeAttr | KeyspacingAttr | OutlinecolorAttr | PositionAttr
        | PostlabelcacheAttr | StatusAttr | TemplateAttr
    )*

private FeatureObject ::= FEATURE FeatureObjectChildren END
private FeatureObjectChildren ::=
    (
        PointsObject
        | ItemsAttr | TextAttr | WktAttr
    )*

private ClusterObject ::= CLUSTER ClusterObjectChildren END // TODO: only within POINT layers
private ClusterObjectChildren ::=
    (
        MaxdistanceAttr | RegionAttr | BufferAttr | GroupAttr | FilterAttr | ProcessingAttr
    )*

private CompositeObject ::= COMPOSITE CompositeObjectChildren END
private CompositeObjectChildren ::=
    (
        OpacityAttr | CompopAttr
    )*

private GridObject ::= GRID GridObjectChildren END
private GridObjectChildren ::=
    (
        LabelformatAttr | MinarcsAttr | MaxarcsAttr | MinintervalAttr | MaxintervalAttr | MinsubdivideAttr
        | MaxsubdivideAttr
    )*

private JoinObject ::= JOIN JoinObjectChildren END
private JoinObjectChildren ::=
    (
        ConnectionAttr | ConnectiontypeJoinAttr | FooterAttr |  FromAttr | HeaderAttr | NameAttr | TableAttr
        | TemplateAttr | ToAttr | TypeJoinAttr
    )*

private QuerymapObject ::= QUERYMAP QuerymapObjectChildren END
private QuerymapObjectChildren ::=
    (
        ColorAttr | SizeAttr | StatusAttr | StyleQuerymapAttr
    )*

private ReferenceObject ::= REFERENCE ReferenceObjectChildren END
private ReferenceObjectChildren ::=
    (
        ColorAttr | ExtentAttr | ImageAttr | MarkerAttr | MarkersizeAttr | MinboxsizeAttr | MaxboxsizeAttr
        | OutlinecolorAttr | SizeAttr | StatusAttr
    )*

private ScalebarObject ::= SCALEBAR ScalebarObjectChildren END
private ScalebarObjectChildren ::=
    (
        LabelObject
        | AlignAttr | BackgroundcolorAttr | ColorAttr | ImagecolorAttr | InterlaceAttr | OutlinecolorAttr | PositionAttr
        | PostlabelcacheAttr | SizeAttr | StatusAttr | StyleScalebarAttr | UnitsAttr
    )*

private PatternObject ::= PATTERN ( integer integer )* END

private ValidationObject ::= VALIDATION ( string string )* END

private MetadataObject ::= METADATA ( string string )* END

private PointsObject ::= POINTS ( double double )* END

private ProjectionObject ::= PROJECTION string* END

//
// ATTRIBUTES
//
private AlignAttr ::= ALIGN AlignEnum {pin=1}
private AnchorpointAttr ::= ANCHORPOINT double double {pin=1} // double 0-1
private AngleAttr ::= ANGLE ( integer | double ) {pin=1}
private AngleClassAttr ::= ANGLE AngleClassEnum {pin=1}
private AngleLabelAttr ::= ANGLE AngleLabelEnum {pin=1}
private AntialiasAttr ::= ANTIALIAS BooleanEnum {pin=1}
private BackgroundcolorAttr ::= BACKGROUNDCOLOR ColorEnum {pin=1} //TODO color
private BrowseformatAttr ::= BROWSEFORMAT string {pin=1}
private BufferAttr ::= BUFFER ( integer | double ) {pin=1}
private CharacterAttr ::= CHARACTER string {pin=1} // TODO char
private ClassgroupAttr ::= CLASSGROUP string {pin=1}
private ClassitemAttr ::= CLASSITEM string {pin=1}
private ColorAttr ::= COLOR ColorEnum {pin=1} //TODO color
private ColorClassAttr ::= COLOR ColorClassEnum {pin=1}
private ConfigAttr ::= CONFIG (
    ( CGI_CONTEXT_URL string )
    | ( MS_ENCRYPTION_KEY string )
    | ( MS_ERRORFILE string )
    | ( MS_NONSQUARE YesNoEnum )
    | ( ON_MISSING_DATA OnMissingDataEnum )
    | ( PROJ_LIB string ) )  {pin=1}
private CompopAttr ::= COMPOP string {pin=1} // TODO: keywords in string
private ConnectionAttr ::= CONNECTION string {pin=1}
private ConnectiontypeAttr ::= CONNECTIONTYPE ConnectiontypeEnum {pin=1}
private ConnectiontypeJoinAttr ::= CONNECTIONTYPE ConnectiontypeJoinEnum {pin=1}
private DataAttr ::= DATA string {pin=1}
private DatapatternAttr ::= DATAPATTERN string {pin=1} // TODO: regular expression
private DebugAttr ::= DEBUG DebugEnum {pin=1}
private DebugClassAttr ::= DEBUG BooleanEnum {pin=1}
private DefresolutionAttr ::= DEFRESOLUTION integer {pin=1}
private DumpAttr ::= DUMP BooleanEnum {pin=1}
private EmptyAttr ::= EMPTY string {pin=1} // TODO: url
private EncodingAttr ::= ENCODING string {pin=1}
private ErrorAttr ::= ERROR string {pin=1} // TODO: url
private ExpressionAttr ::= EXPRESSION ExpressionEnum {pin=1}
private ExtentAttr ::= EXTENT ( integer | double ) ( integer | double ) ( integer | double ) ( integer | double ) {pin=1}
private FilledAttr ::= FILLED BooleanEnum {pin=1}
private FilterAttr ::= FILTER ExpressionEnum {pin=1} // TODO: expression!
private FilteritemAttr ::= FILTERITEM string {pin=1}
private FontAttr ::= FONT string {pin=1} // TODO must be defined in fontset!
private FontLabelAttr ::= FONT ( string | attributeToken ) {pin=1} // TODO must be defined in fontset!
private FontsetAttr ::= FONTSET string {pin=1} // TODO: file
private FooterAttr ::= FOOTER string {pin=1} // TODO: filename
private ForceAttr ::= FORCE BooleanEnum {pin=1}
private FromAttr ::= FROM string {pin=1}
private GapAttr ::= GAP double {pin=1}
private GeomtransformAttr ::= GEOMTRANSFORM string {pin=1} // TODO: [<expression>|<Javascript file>]
private GeomtransformClassAttr ::= GEOMTRANSFORM GeomtransformClassEnum {pin=1}
private GridstepAttr ::= GRIDSTEP integer {pin=1}
private GroupAttr ::= GROUP string {pin=1}
private HeaderAttr ::= HEADER string {pin=1} // TODO: filename
private ImageAttr ::= IMAGE string {pin=1}
private ImagecolorAttr ::= IMAGECOLOR ColorEnum {pin=1} // TODO: hex or RGB
private ImagepathAttr ::= IMAGEPATH string {pin=1} // TODO: path
private ImagetypeAttr ::= IMAGETYPE ImagetypeEnum {pin=1} // TODO: must match one of the defined ones in OutputfomatObject
private ImageurlAttr ::= IMAGEURL string {pin=1} // TODO path? url?
private InitialgapAttr ::= INITIALGAP integer {pin=1}
private InterlaceAttr ::= INTERLACE OnOffEnum {pin=1}
private ItemsAttr ::= ITEMS string {pin=1} // TODO: comma separated list
private KeyimageAttr ::= KEYIMAGE string {pin=1} // TODO filename
private KeysizeAttr ::= KEYSIZE integer integer {pin=1}
private KeyspacingAttr ::= KEYSPACING integer integer {pin=1}
private LabelcacheAttr ::= LABELCACHE OnOffEnum {pin=1}
private LabelformatAttr ::= LABELFORMAT LabelFormatEnum {pin=1}
private LabelitemAttr ::= LABELITEM string {pin=1}
private LabelmaxscaledenomAttr ::= LABELMAXSCALEDENOM ( integer | double ) {pin=1}
private LabelminscaledenomAttr ::= LABELMINSCALEDENOM ( integer | double ) {pin=1}
private LabelrequiresAttr ::= LABELREQUIRES string {pin=1} // TODO: expression
private LegendformatAttr ::= LEGENDFORMAT string {pin=1}
private LinecapAttr ::= LINECAP LinecapEnum {pin=1}
private LinejoinAttr ::= LINEJOIN LinejoinEnum {pin=1}
private LinejoinmaxsizeAttr ::= LINEJOINMAXSIZE integer {pin=1}
private MarkerAttr ::= MARKER ( integer | string) {pin=1}
private MarkersizeAttr ::= MARKERSIZE integer {pin=1}
private MaskAttr ::= MASK string {pin=1} // TODO: layername validation
private MaxarcsAttr ::= MAXARCS double {pin=1}
private MaxboxsizeAttr ::= MAXBOXSIZE integer {pin=1}
private MaxdistanceAttr ::= MAXDISTANCE ( integer | double ) {pin=1}
private MaxfeaturesAttr ::= MAXFEATURES integer {pin=1}
private MaxgeowidthAttr ::= MAXGEOWIDTH ( integer | double ) {pin=1}
private MaxintervalAttr ::= MAXINTERVAL ( integer | double ) {pin=1}
private MaxlengthAttr ::= MAXLENGTH integer {pin=1}
private MaxoverlapangleAttr ::= MAXOVERLAPANGLE ( integer | double ) {pin=1}
private MaxscaledenomAttr ::= MAXSCALEDENOM ( integer | double ) {pin=1}
private MaxsizeAttr ::= MAXSIZE integer {pin=1}
private MaxsubdivideAttr ::= MAXSUBDIVIDE ( integer | double ) {pin=1}
private MaxtemplateAttr ::= MAXTEMPLATE string {pin=1} // TODO: [file/url]
private MaxwidthAttr ::= MAXWIDTH ( integer | double ) {pin=1}
private MinarcsAttr ::= MINARCS ( integer | double ) {pin=1}
private MinboxsizeAttr ::= MINBOXSIZE integer {pin=1}
private MindistanceAttr ::= MINDISTANCE ( integer | double ) {pin=1}
private MinfeaturesizeAttr ::= MINFEATURESIZE MinfeaturesizeEnum {pin=1}
private MingeowidthAttr ::= MINGEOWIDTH ( integer | double ) {pin=1}
private MinintervalAttr ::= MININTERVAL ( integer | double ) {pin=1}
private MinscaledenomAttr ::= MINSCALEDENOM ( integer | double ) {pin=1}
private MinsizeAttr ::= MINSIZE integer {pin=1}
private MinsubdivideAttr ::= MINSUBDIVIDE ( integer | double ) {pin=1}
private MintemplateAttr ::= MINTEMPLATE string {pin=1} // TODO: [file/url]
private MinwidthAttr ::= MINWIDTH ( integer | double ) {pin=1}
NameAttr ::= NAME string {pin=1} // TODO: Unique check
private OffsetAttr ::= OFFSET ( integer | double ) ( integer | double ) {pin=1}
private OffsiteAttr ::= OFFSITE ColorEnum {pin=1}
private OpacityAttr ::= OPACITY integer {pin=1} // TODO: 0-100
private OpacityClassAttr ::= OPACITY ( integer | attributeToken ) {pin=1}
private OutlinecolorAttr ::= OUTLINECOLOR ColorEnum {pin=1} // TODO color
private OutlinecolorClassAttr ::= OUTLINECOLOR ColorClassEnum {pin=1}
private OutlinewidthAttr ::= OUTLINEWIDTH ( double | attributeToken ) {pin=1}
private PartialsAttr ::= PARTIALS BooleanEnum {pin=1}
private PluginAttr ::= PLUGIN string {pin=1} // TODO: filename
private PolaroffsetAttr ::= POLAROFFSET ( ( double | attributeToken ) ( double | attributeToken ) ) {pin=1}
private PositionAttr ::= POSITION PositionEnum {pin=1}
private PostlabelcacheAttr ::= POSTLABELCACHE BooleanEnum {pin=1}
private PriorityAttr ::= PRIORITY ( integer | string | attributeToken ) {pin=1}
private ProcessingAttr ::= PROCESSING string {pin=1} // TODO: keywords in string!!
private QueryformatAttr ::= QUERYFORMAT string {pin=1}
private RegionAttr ::= REGION string {pin=1}
private RepeatdistanceAttr ::= REPEATDISTANCE integer {pin=1}
private RequiresAttr ::= REQUIRES string {pin=1} // TODO: expression
private ResolutionAttr ::= RESOLUTION integer {pin=1}
private ScaledenomAttr ::= SCALEDENOM ( integer | double ) {pin=1}
private ShadowcolorAttr ::= SHADOWCOLOR ColorEnum {pin=1}
private ShadowsizeAttr ::= SHADOWSIZE ( ( integer | attributeToken ) ( integer | attributeToken ) ) {pin=1}
private ShapepathAttr ::= SHAPEPATH string {pin=1} // TODO: directory
private SizeAttr ::= SIZE integer integer {pin=1}
private SizeClassAttr ::= SIZE ( double | attributeToken ) {pin=1}
private SizeLabelAttr ::= SIZE SizeLabelEnum {pin=1}
private SizeunitsAttr ::= SIZEUNITS SizeUnitEnum {pin=1}
StatusAttr ::= STATUS StatusEnum {pin=1}
private StyleitemAttr ::= STYLEITEM string {pin=1} // TODO: [<attribute>|auto|<javascript file>]
private StyleQuerymapAttr ::= STYLE StyleQuerymapEnum {pin=1}
private StyleScalebarAttr ::= STYLE ( 0 | 1) {pin=1}
private SymbolClassAttr ::= SYMBOL (integer | string | attributeToken) {pin=1}
private SymbolscaledenomAttr ::= SYMBOLSCALEDENOM double {pin=1}
private SymbolsetAttr ::= SYMBOLSET string {pin=1} // TODO: file
private TableAttr ::= TABLE string {pin=1}
private TemplateAttr ::= TEMPLATE string {pin=1} // TODO: [file|url]
private TemplatepatternAttr ::= TEMPLATEPATTERN string {pin=1} // TODO regex
private TemppathAttr ::= TEMPPATH string {pin=1} // TODO: path
private TextAttr ::= TEXT string {pin=1} // TODO string,expression
private TileindexAttr ::= TILEINDEX string {pin=1} // TODO: [filename|layername]
private TileitemAttr ::= TILEITEM string {pin=1} // TODO: [attribute]
private TilesrsAttr ::= TILESRS string {pin=1} // TODO: [attribute]
private TitleAttr ::= TITLE string {pin=1}
private ToAttr ::= TO string {pin=1}
private ToleranceAttr ::= TOLERANCE ( integer | double ) {pin=1}
private ToleranceunitsAttr ::= TOLERANCEUNITS ToleranceUnitEnum {pin=1}
private TransformAttr ::= TRANSFORM TransformEnum {pin=1}
private TransparentAttr ::= TRANSPARENT integer {pin=1} // TODO color-index
private TypeAttr ::= TYPE TypeEnum {pin=1}
private TypeJoinAttr ::= TYPE TypeJoinEnum {pin=1}
private TypeLabelAttr ::= TYPE TypeLabelEnum {pin=1}
private TypeSymbolAttr ::= TYPE TypeSymbolEnum {pin=1}
private UnitsAttr ::= UNITS UnitsEnum {pin=1}
private UtfdataAttr ::= UTFDATA string {pin=1}
private UtfitemAttr ::= UTFITEM string {pin=1}
private WidthClassAttr ::= WIDTH ( ( integer | double ) | attributeToken ) {pin=1}
private WktAttr ::= WKT string {pin=1} // TODO wkt standard
private WrapAttr ::= WRAP string {pin=1} // TODO character

//
// ENUMERATIONS
//
private AlignEnum ::= left|center|right
private AngleClassEnum ::= integer|double|attributeToken|auto
private AngleLabelEnum ::= AngleClassEnum|auto2|follow
private BooleanEnum ::= true|false
private ColorClassEnum ::= ColorEnum|attributeToken
private ColorEnum ::= string | ( integer integer integer )
private ConnectiontypeEnum ::= contour|kerneldensity|local|ogr|oraclespatial|plugin|postgis|sde|union|uvraster|wfs|wms // TODO check for completeness
private ConnectiontypeJoinEnum ::= csv|mysql|postgresql
private DebugEnum ::= OnOffEnum|0|1|2|3|4|5
private ExpressionEnum ::= ( string | logicalExpressionToken | regexToken | listExpressionToken ) // TODO string functions
private GeomtransformClassEnum ::= bbox|centroid|end|labelpnt|labelpoly|start|vertices|string // TODO string = expression!
private ImagetypeEnum ::= png|gif|png8|jpeg|svg|pdf|gtiff|kml|kmz|cairopng|string
private LabelFormatEnum ::= dd|ddmm|ddmmss|string // TODO string=cformat-string
private LinecapEnum ::= butt|round|square
private LinejoinEnum ::= round|miter|bevel|none
private MinfeaturesizeEnum ::= integer|auto
private OnMissingDataEnum ::= fail|log|ignore
private OnOffEnum ::= on|off
private PositionEnum ::= ul|uc|ur|cl|cc|cr|ll|lc|lr // TODO why not cl\cc\cr?
private SizeLabelEnum ::= integer|tiny|small|medium|large|giant|attributeToken
private SizeUnitEnum ::= feet|inches|kilometers|meters|miles|nauticalmiles|pixels
private StatusEnum ::= OnOffEnum|default
private StyleQuerymapEnum ::= normal|hilite|selected
private ToleranceUnitEnum ::= SizeUnitEnum|dd
private TransformEnum ::= BooleanEnum|ul|uc|ur|cl|cc|cr|ll|lc|lr
private TypeEnum ::= chart|circle|line|point|polygon|raster|query
private TypeJoinEnum ::= ONE-TO-ONE|ONE-TO-MANY
private TypeLabelEnum ::= bitmap|truetype
private TypeSymbolEnum ::= ellipse|hatch|pixmap|svg|truetype|vector
private UnitsEnum ::= ToleranceUnitEnum|percentages
private YesNoEnum ::= yes|no
