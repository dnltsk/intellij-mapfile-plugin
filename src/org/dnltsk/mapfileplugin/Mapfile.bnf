{
    parserClass="org.dnltsk.mapfileplugin.parser.MapfileParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Mapfile"
    psiImplClassSuffix="Impl"
    psiPackage="org.dnltsk.mapfileplugin.psi"
    psiImplPackage="org.dnltsk.mapfileplugin.psi.impl"

    elementTypeHolderClass="org.dnltsk.mapfileplugin.psi.MapfileTypes"
    elementTypeClass="org.dnltsk.mapfileplugin.psi.MapfileElementType"
    tokenTypeClass="org.dnltsk.mapfileplugin.psi.MapfileTokenType"

    psiImplUtilClass="org.dnltsk.mapfileplugin.psi.impl.MapfilePsiImplUtil"

  tokens=[
    SEMI=';'
    EQ='='
    LP='('
    RP=')'

    space='regexp:\s+'
    comment='regexp:#.*'
    number='regexp:[+-]{0,1}\d+(\.\d*)?'
    id='regexp:\p{Alpha}\w*'
    string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"

    op_1='+'
    op_2='-'
    op_3='*'
    op_4='/'
    op_5='!'
  ]
}

mapfile ::= (MapObject | LayerObject)

//
// OBJECTS
//
private MapObject ::= MAP MapAttributes END
private MapAttributes ::=
    (
        LayerObject | LegendObject | ProjectionObject | QuerymapObject | ReferenceObject | ScalebarObject
        | SymbolObject | WebObject

        | AngleAttr | ConfigAttr | DatapatternAttr | DebugAttr | DefresolutionAttr | ExtentAttr | FontsetAttr
        | ImagecolorAttr | ImagetypeAttr | InterlaceAttr | MaxsizeAttr | NameAttr | ResolutionAttr
        | ScaledenomAttr | ShapepathAttr | SizeAttr | StatusAttr | SymbolsetAttr | TemplatepatternAttr | UnitsAttr

    ) MapAttributes*

private LayerObject ::= LAYER LayerAttributes END
private LayerAttributes ::=
    (
        ClassObject | ClusterObject | CompositeObject | FeatureObject | GridObject | JoinObject | MetadataObject
        | ProjectionObject | ValidationObject

        | ClassgroupAttr | ClassitemAttr | ConnectionAttr | ConnectiontypeAttr | DataAttr | DebugAttr | DumpAttr
        | EncodingAttr | ExtentAttr | FilterAttr | FooterAttr | FilteritemAttr | GeomtransformAttr | GroupAttr
        | HeaderAttr | LabelcacheAttr | LabelitemAttr | LabelmaxscaledenomAttr | LabelminscaledenomAttr
        | LabelrequiresAttr | MaskAttr | MaxfeaturesAttr | MaxgeowidthAttr | MaxscaledenomAttr | MingeowidthAttr
        | MinscaledenomAttr | NameAttr | PluginAttr | PostlabelcacheAttr | ProcessingAttr | OffsiteAttr | SizeunitsAttr
        | RequiresAttr | StatusAttr | StyleitemAttr | SymbolscaledenomAttr | TemplateAttr | TileindexAttr
        | TileitemAttr | TilesrsAttr | TitleAttr | ToleranceunitsAttr | ToleranceAttr | TransformAttr | TypeAttr
        | UnitsAttr | UtfdataAttr | UtfitemAttr

    ) LayerAttributes*

private ClassObject ::= CLASS END

private SymbolObject ::= SYMBOL END

private LabelObject ::= LABEL END

private ClusterObject ::= CLUSTER ClusterAttributes END // TODO: only within POINT layers
private ClusterAttributes ::=
    (
        MaxdistanceAttr | RegionAttr | BufferAttr | GroupAttr | FilterAttr | ProcessingAttr
    ) ClusterAttributes*

private CompositeObject ::= COMPOSITE CompositeAttributes END
private CompositeAttributes ::=
    (
        OpacityAttr | CompopAttr
    ) CompositeAttributes*

private FeatureObject ::= FEATURE FeatureAttributes END
private FeatureAttributes ::=
    (
        PointsObject

        | ItemsAttr | TextAttr | WktAttr
    ) FeatureAttributes*

private PointsObject ::= POINTS (number number)* END

private GridObject ::= GRID GridAttributes END
private GridAttributes ::=
    (
        LabelformatAttr | MinarcsAttr | MaxarcsAttr | MinintervalAttr | MaxintervalAttr | MinsubdivideAttr
        | MaxsubdivideAttr
    ) GridAttributes*

private JoinObject ::= JOIN JoinAttributes END
private JoinAttributes ::=
    (
        ConnectionAttr | ConnectiontypeJoinAttr | FooterAttr |  FromAttr | HeaderAttr | NameAttr | TableAttr
        | TemplateAttr | ToAttr | TypeJoinAttr
    ) JoinAttributes*

private LegendObject ::= LEGEND LegendAttributes END
private LegendAttributes ::=
    (
        LabelObject

        | ImagecolorAttr | InterlaceAttr | KeysizeAttr | KeyspacingAttr | OutlinecolorAttr | PositionAttr
        | PostlabelcacheAttr | StatusAttr | TemplateAttr
    ) LegendAttributes*

private QuerymapObject ::= QUERYMAP QuerymapAttributes END
private QuerymapAttributes ::=
    (
        ColorAttribute | SizeAttr | StatusAttr | StyleQuerymapAttr
    ) QuerymapAttributes*

private ReferenceObject ::= REFERENCE END

private MetadataObject ::= METADATA END

private ProjectionObject ::= PROJECTION string* END

private ScalebarObject ::= SCALEBAR END

private ValidationObject ::= VALIDATION END

private WebObject ::= WEB WebAttributes END
private WebAttributes ::=
    (
        MetadataObject | ValidationObject

        | BrowseformatAttr | EmptyAttr | ErrorAttr | FooterAttr | HeaderAttr | ImagepathAttr | ImageurlAttr
        | LegendformatAttr | MaxscaledenomAttr | MaxtemplateAttr | MinscaledenomAttr | MintemplateAttr
        | QueryformatAttr | TemplateAttr | TemppathAttr
    ) WebAttributes*

//
// ATTRIBUTES
//
private AngleAttr ::= ANGLE number
private BrowseformatAttr ::= BROWSEFORMAT string
private BufferAttr ::= BUFFER number
private ClassgroupAttr ::= CLASSGROUP string
private ClassitemAttr ::= CLASSITEM string
private ColorAttribute ::= ( string | ( number number number ) ) //TODO color
private ConfigAttr ::= CONFIG (
    ( CGI_CONTEXT_URL string )
    | ( MS_ENCRYPTION_KEY string )
    | ( MS_ERRORFILE string )
    | ( MS_NONSQUARE yesNoValues )
    | ( ON_MISSING_DATA onMissingDataValues )
    | ( PROJ_LIB string ) )
private ConnectionAttr ::= CONNECTION string
private ConnectiontypeAttr ::= CONNECTIONTYPE connectiontypeValues
private ConnectiontypeJoinAttr ::= CONNECTIONTYPE connectiontypeJoinValues
private CompopAttr ::= COMPOP string // TODO: keywords in string
private DataAttr ::= DATA string
private DatapatternAttr ::= DATAPATTERN string // TODO: regular expression
private DebugAttr ::= DEBUG debugValues
private DefresolutionAttr ::= DEFRESOLUTION number // TODO: int
private DumpAttr ::= DUMP booleanValues
private EmptyAttr ::= EMPTY string // TODO: url
private EncodingAttr ::= ENCODING string
private ErrorAttr ::= ERROR string // TODO: url
private ExtentAttr ::= EXTENT number number number number
private FilterAttr ::= FILTER string // TODO: expression!
private FilteritemAttr ::= FILTERITEM string
private FontsetAttr ::= FONTSET string // TODO: file
private FooterAttr ::= FOOTER string // TODO: filename
private FromAttr ::= FROM string
private GeomtransformAttr ::= GEOMTRANSFORM string // TODO: [<expression>|<Javascript file>]
private GroupAttr ::= GROUP string
private HeaderAttr ::= HEADER string // TODO: filename
private ImagecolorAttr ::= IMAGECOLOR ( string | ( number number number ) ) // TODO: hex or RGB
private ImagepathAttr ::= IMAGEPATH string // TODO: path
private ImagetypeAttr ::= IMAGETYPE imagetypeValues // TODO: must match one of the defined ones in OutputfomatObject
private ImageurlAttr ::= IMAGEURL string // TODO path? url?
private InterlaceAttr ::= INTERLACE onOffValues
private ItemsAttr ::= ITEMS string // TODO: comma separated list
private KeysizeAttr ::= KEYSIZE number number // TODO int?
private KeyspacingAttr ::= KEYSPACING number number // TODO int?
private LabelcacheAttr ::= LABELCACHE onOffValues
private LabelformatAttr ::= LABELFORMAT labelFormatValues
private LabelitemAttr ::= LABELITEM string
private LabelmaxscaledenomAttr ::= LABELMAXSCALEDENOM number
private LabelminscaledenomAttr ::= LABELMINSCALEDENOM number
private LabelrequiresAttr ::= LABELREQUIRES string // TODO: expression
private LegendformatAttr ::= LEGENDFORMAT string
private MaskAttr ::= MASK string // TODO: layername validation
private MaxarcsAttr ::= MAXARCS number
private MaxdistanceAttr ::= MAXDISTANCE number
private MaxfeaturesAttr ::= MAXFEATURES number
private MaxgeowidthAttr ::= MAXGEOWIDTH number
private MaxintervalAttr ::= MAXINTERVAL number
private MaxscaledenomAttr ::= MAXSCALEDENOM number
private MaxsubdivideAttr ::= MAXSUBDIVIDE number
private MaxsizeAttr ::= MAXSIZE number // TODO: int
private MaxtemplateAttr ::= MAXTEMPLATE string // TODO: [file/url]
private MinarcsAttr ::= MINARCS number
private MingeowidthAttr ::= MINGEOWIDTH number
private MinintervalAttr ::= MININTERVAL number
private MinscaledenomAttr ::= MINSCALEDENOM number
private MinsubdivideAttr ::= MINSUBDIVIDE number
private MintemplateAttr ::= MINTEMPLATE string // TODO: [file/url]
private NameAttr ::= NAME string // TODO: Unique check
private OffsiteAttr ::= OFFSITE ( string | (number number number) ) // TODO is this a color
private OpacityAttr ::= OPACITY // TODO: integer
private OutlinecolorAttr ::= OUTLINECOLOR ( string | (number number number) ) // TODO color
private PluginAttr ::= PLUGIN string // TODO: filename
private PositionAttr ::= POSITION positionValues
private PostlabelcacheAttr ::= POSTLABELCACHE booleanValues
private ProcessingAttr ::= PROCESSING string // TODO: keywords in string!!
private QueryformatAttr ::= QUERYFORMAT string
private RegionAttr ::= REGION string
private RequiresAttr ::= REQUIRES string // TODO: expression
private ResolutionAttr ::= RESOLUTION number // TODO: int
private ScaledenomAttr ::= SCALEDENOM number
private ShapepathAttr ::= SHAPEPATH string // TODO: directory
private SizeAttr ::= SIZE number number // TODO: int
private SizeunitsAttr ::= SIZEUNITS sizeUnitValues
private StatusAttr ::= STATUS statusValues
private StyleQuerymapAttr ::= STYLE styleQuerymapValues
private StyleitemAttr ::= STYLEITEM string // TODO: [<attribute>|auto|<javascript file>]
private SymbolscaledenomAttr ::= SYMBOLSCALEDENOM number
private SymbolsetAttr ::= SYMBOLSET string  // TODO: file
private TableAttr ::= TABLE string
private TemplateAttr ::= TEMPLATE string // TODO: [file|url]
private TemplatepatternAttr ::= TEMPLATEPATTERN string // TODO regex
private TemppathAttr ::= TEMPPATH string // TODO: path
private TextAttr ::= TEXT string
private TileindexAttr ::= TILEINDEX string // TODO: [filename|layername]
private TileitemAttr ::= TILEITEM string // TODO: [attribute]
private TilesrsAttr ::= TILESRS string // TODO: [attribute]
private TitleAttr ::= TITLE string
private ToAttr ::= TO string
private ToleranceAttr ::= TOLERANCE number
private ToleranceunitsAttr ::= TOLERANCEUNITS toleranceUnitValues
private TransformAttr ::= TRANSFORM transformValues
private TypeAttr ::= TYPE typeValues
private TypeJoinAttr ::= TYPE typeJoinValues
private UnitsAttr ::= UNITS unitsValues
private UtfdataAttr ::= UTFDATA string
private UtfitemAttr ::= UTFITEM string
private WktAttr ::= WKT string // TODO wkt standard

//
// VALUES
//
private booleanValues ::= true|false
private connectiontypeValues ::= contour|kerneldensity|local|ogr|oraclespatial|plugin|postgis|sde|union|uvraster|wfs|wms // TODO check for completeness
private connectiontypeJoinValues ::= csv|mysql|postgresql
private debugValues ::= onOffValues|0|1|2|3|4|5
private imagetypeValues ::= png|gif|png8|jpeg|svg|pdf|GTiff|kml|kmz|cairopng|string
private labelFormatValues ::= dd|ddmm|ddmmss|string // TODO string=cformat-string
private onMissingDataValues ::= fail|log|ignore
private onOffValues ::= on|off
private positionValues ::= ul|uc|ur|ll|lc|lr // TODO why not cl\cc\cr?
private sizeUnitValues ::= feet|inches|kilometers|meters|miles|nauticalmiles|pixels
private statusValues ::= onOffValues|default
private styleQuerymapValues ::= normal|hilite|selected
private toleranceUnitValues ::= sizeUnitValues|dd
private transformValues ::= booleanValues|ul|uc|ur|cl|cc|cr|ll|lc|lr
private typeValues ::= chart|circle|line|point|polygon|raster|query
private typeJoinValues ::= ONE-TO-ONE|ONE-TO-MANY
private unitsValues ::= toleranceUnitValues|percentages
private yesNoValues ::= yes|no
